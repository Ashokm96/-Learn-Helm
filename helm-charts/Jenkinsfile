pipeline {
    agent { label "chart-testing-agent" }
    options {
        buildDiscarder(logRotator(numToKeepStr: '10'))
    }
    stages {
        stage("Lint") {
            steps {
                container("chart-testing") {
                    sh "ct lint"
                }
            }
        }
        stage("Install") {
            steps {
                container("chart-testing") {
                    sh "ct install --upgrade"
                }
            }
        }
        stage("Package Charts") {
            steps {
                script {
                    container("chart-testing") {
                        // Create folder for packaged charts
                        sh "mkdir packaged-charts"

                        // Return all of the Helm charts under the "helm-charts/charts" folder and split into a list
                        charts = sh(script: "find helm-charts/charts ! -path helm-charts/charts -type d -maxdepth 1 -print", returnStdout: true)
                        chartList = charts.split("\n")

                        // Package each chart in the list and move to the packaged-charts folder
                        chartList.each { chart ->
                            sh "helm package ${chart} --dependency-update --destination packaged-charts"
                        }
                    }
                }
            }
        }
        stage("Push Charts to Chart Repo") {
            steps {
                script {
                    container("chart-testing") {
                        // Clone GitHub Pages repository to a folder called "chart-repo"
                        sh "git clone ${env.GITHUB_PAGES_REPO_URI} chart-repo"

                        // Determine if these charts should be pushed to "stable" or "staging" based on the branch
                        def repoType
                        if (env.BRANCH_NAME == "master") {
                            repoType = "stable"
                        } else {
                            repoType = "staging"
                        }

                        // Create the corresponding "stable" or "staging" folder if it does not exist
                        def files = sh(script: "ls chart-repo", returnStdout: true)
                        if (!files.contains(repoType)) {
                            sh "mkdir chart-repo/${repoType}"
                        }

                        // Move charts from the packaged-charts folder to the corresponding "stable" or "staging" folder
                        sh "mv packaged-charts/*.tgz chart-repo/${repoType}"

                        // Generate the updated index.yaml
                        sh "helm repo index chart-repo/${repoType} --merge packaged-charts/index.yaml"

                        // Update git config details
                        sh "git config --global user.email 'chartrepo-robot@example.com'"
                        sh "git config --global user.name 'chartrepo-robot'"

                        dir("chart-repo") {

                            // Determine if any charts were changed or added
                            def status = sh(script: "git status -s", returnStdout: true)
                            if (status.contains("tgz") || status.contains("staging/\n") || status.contains("stable/\n")) {

                                // Add and commit the changes
                                sh "git add --all"
                                sh "git commit -m 'pushing charts from branch ${env.BRANCH_NAME}'"
                                withCredentials([usernameColonPassword(credentialsId: 'github-auth', variable: 'USERPASS')]) {
                                    script {

                                        // Inject GitHub auth and push to the master branch, where the charts are being served
                                        def authRepo = env.GITHUB_PAGES_REPO_URI.replace("://", "://${USERPASS}@")
                                        sh "git push ${authRepo} master"
                                    }
                                }
                            } else {
                                echo "No charts changed! Skipping push..."
                            }
                        }
                    }
                }
            }
        }
    }
}